(load "lisp/bt_goal_seek.mueslisp")

(define (nth xs idx)
  (if (= idx 0)
      (car xs)
      (nth (cdr xs) (- idx 1))))

(define (max2 a b)
  (if (> a b) a b))

(define (make-action left right)
  (begin
    (define action (map.make))
    (map.set! action 'action_schema "epuck.action.v1")
    (map.set! action 'u (list left right))
    action))

(define (branch-path branch-id)
  (if (= branch-id 1)
      (list "root" "collision" "avoid")
      (if (= branch-id 2)
          (list "root" "goal" "plan_goal")
          (list "root" "roam"))))

(define (make-bt-map branch-id)
  (begin
    (define bt (map.make))
    (define status-by-node (map.make))
    (map.set! bt 'active_path (branch-path branch-id))
    (map.set! status-by-node "root" "running")
    (map.set! status-by-node "branch" (if (= branch-id 1) "avoid" (if (= branch-id 2) "goal" "roam")))
    (map.set! bt 'status_by_node status-by-node)
    bt))

(define (topk-entry->u row)
  (begin
    (define out (map.make))
    (map.set! out 'u (map.get row "action" (list 0.0 0.0)))
    (map.set! out 'visits (map.get row "visits" 0))
    (map.set! out 'q (map.get row "q" 0.0))
    out))

(define (topk->u rows)
  (if (null? rows)
      nil
      (cons (topk-entry->u (car rows)) (topk->u (cdr rows)))))

(define (planner-unused)
  (begin
    (define planner (map.make))
    (map.set! planner 'used #f)
    (map.set! planner 'confidence 0.0)
    planner))

(define (planner-from-meta planner-meta)
  (begin
    (define planner (map.make))
    (map.set! planner 'used #t)
    (map.set! planner 'budget_ms (map.get planner-meta "budget_ms" 0))
    (map.set! planner 'time_used_ms (map.get planner-meta "time_used_ms" 0.0))
    (map.set! planner 'iters (map.get planner-meta "iters" 0))
    (map.set! planner 'root_visits (map.get planner-meta "root_visits" 0))
    (map.set! planner 'root_children (map.get planner-meta "root_children" 0))
    (map.set! planner 'widen_added (map.get planner-meta "widen_added" 0))
    (map.set! planner 'confidence (map.get planner-meta "confidence" 0.0))
    (map.set! planner 'top_k (topk->u (map.get planner-meta "top_k" nil)))
    planner))

(define roam-rng (rng.make 848484))

(define (compute-roam-action)
  (begin
    (define speed (rng.uniform roam-rng 2.0 2.8))
    (define turn (rng.uniform roam-rng -1.0 1.0))
    (list (clamp (+ speed turn) -6.28 6.28)
          (clamp (- speed turn) -6.28 6.28))))

(define (compute-avoid-action proximity obstacle-front)
  (begin
    (define right (+ (+ (nth proximity 0) (nth proximity 1)) (nth proximity 2)))
    (define left (+ (+ (nth proximity 5) (nth proximity 6)) (nth proximity 7)))
    (define turn (+ 1.4 (* 3.4 (clamp obstacle-front 0.0 1.0))))
    (if (> right left)
        (list (- turn) turn)
        (list turn (- turn)))))

(define (compute-goal-action goal-dist goal-bearing obstacle-front)
  (begin
    (define base (clamp (+ 2.1 (* 2.0 (clamp goal-dist 0.0 1.2))) 1.4 5.2))
    (define steer (clamp (+ (* 3.0 goal-bearing) (* 1.8 obstacle-front)) -2.0 2.0))
    (list (clamp (+ base steer) -6.28 6.28)
          (clamp (- base steer) -6.28 6.28))))

(define (on_tick obs)
  (begin
    (define proximity (map.get obs 'proximity (list 0 0 0 0 0 0 0 0)))
    (define goal-dist (map.get obs 'goal_dist 1.0))
    (define goal-bearing (map.get obs 'goal_bearing 0.0))
    (define obstacle-front (map.get obs 'obstacle_front (- 1.0 (map.get obs 'min_obstacle 1.0))))

    (define collision-imminent (or (> obstacle-front 0.45) (< (map.get obs 'min_obstacle 1.0) 0.56)))
    (define goal-available (< goal-dist 2.5))

    (define avoid-action (compute-avoid-action proximity obstacle-front))
    (define goal-action (compute-goal-action goal-dist goal-bearing obstacle-front))
    (define roam-action (compute-roam-action))

    (define tick-inputs
      (list
        (list 'collision_imminent collision-imminent)
        (list 'goal_available goal-available)
        (list 'act_avoid avoid-action)
        (list 'act_roam roam-action)
        (list 'planner_state (list goal-dist goal-bearing obstacle-front))
        (list 'planner_prior goal-action)))

    (bt.tick inst tick-inputs)

    (define bt-action (bt.blackboard.get inst 'action_vec roam-action))
    (define branch-id (bt.blackboard.get inst 'active_branch 3))

    (define planner-meta-raw (bt.blackboard.get inst 'planner_meta "{}"))
    (define planner-meta (json.decode planner-meta-raw))
    (define planner-confidence (map.get planner-meta "confidence" 0.0))

    (define action-vec
      (if (= branch-id 2)
          (if (< planner-confidence 0.04)
              goal-action
              bt-action)
          bt-action))

    (define out (map.make))
    (map.set! out 'schema_version "epuck_demo.v1")
    (map.set! out 'action (make-action (nth action-vec 0) (nth action-vec 1)))
    (map.set! out 'bt (make-bt-map branch-id))
    (map.set! out 'planner (if (= branch-id 2) (planner-from-meta planner-meta) (planner-unused)))
    (map.set! out 'done (< goal-dist 0.08))
    out))

(env.attach "webots")

(define env-cfg (map.make))
(map.set! env-cfg 'demo "goal")
(map.set! env-cfg 'obs_schema "epuck.goal.obs.v1")
(map.set! env-cfg 'tick_hz 20)
(map.set! env-cfg 'steps_per_tick 1)
(map.set! env-cfg 'realtime #t)
(env.configure env-cfg)

(define inst (bt.new-instance goal-tree))
(bt.export-dot goal-tree "out/tree.dot")

(define safe-action (make-action 0.0 0.0))
(define run-cfg (map.make))
(map.set! run-cfg 'tick_hz 20)
(map.set! run-cfg 'max_ticks 3200)
(map.set! run-cfg 'safe_action safe-action)
(map.set! run-cfg 'realtime #t)
(map.set! run-cfg 'log_path "logs/goal.jsonl")
(map.set! run-cfg 'schema_version "epuck_demo.v1")

(env.run-loop run-cfg on_tick)
